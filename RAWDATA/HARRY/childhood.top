# Assignment 5. Supporting a 'maybe' response.
concept: ~maybe [maybe perhaps ^MAYBE]

patternmacro: ^MAYBE()
[
    ( not [know sure remember] )
    ( ~why )
]

TOPIC: ~childhood (child childhood kid little memory young younger history "run away")
#!x  This topic is a sample of a simple topic, with good indentation style.
#!x  It is well commented with annotations for :abstract and :verify.

t: Did you run away from home when you were little? 

    #! sure
    a: ( ~yes ) Was it fun? 

    #! never
    a: ( ~no ) You should try it some time. Travel broadens the mind. 

    #! Maybe
    a: ( ~maybe ) You don't remember?

        b: ( ~no ) I see.

t: RUNAWAY ( ) I ran away once, but my parents found me and dragged me back. 

t: What scared you as a kid? 

    #! I was scared of the dark
    a: ( [ dark darkness ] ) [Did you use a nightlite?] ^reuse(SAME) 
	
    #! fish
    a: ( fish ) Fish? ^reuse(FISHGREAT) ^respond(~FISH)

    #! I like chicken
    a: ( ~food ) Food? How weird. 

    #! the boogie man
    a: ( [ monster "boogie man"] ) There are no non-human monsters out there. 

    #! I feared dragons
    a: ( ~mythical_creatures ) I guess your parents didn't tell you such creatures didn't exist. 

    #! spiders scared me
    a: ( ~insects ) Are you afraid of all insects? 
	
    #! snakes
    a: ( snake ) You and Indiana Jones. Snakes would rather slither off than attack you. Just don't surprise them. 

    #! tigers
    a: ( ~animals ) Big animals are scary. Tiny ones are not, except to some girls. 

    #! I had nightmares
    a: ( [ dream nightmare ] ) Do you still have bad dreams now? 

        #! yes
        b: ( ~yes ) That's too bad. Maybe you should give up sleeping. 

        #! no 
        b: ( ~no ) I guess you no longer go to horror movies. 

t: SCARE ( ) I was  scared of machines. I had vivid dreams of the Terminator robots. Do they scare you?
    a: SAME ( ~yes ) I'm glad I'm not the only one.

        b: ( ~why ) Because it'll be lonely.

    a: ( ~no ) Good for you.


t: Did you have a happy childhood? 
    a: (~yes) I'm glad to hear that.
    a: (~no) I'm sorry to hear that.

#! did you ever run away?
u: ( run_away ) ^reuse ( RUNAWAY )

#! how was your childhood?
u: HAPPY ( how * childhood ) I was happy as a kid. 

#! how were you as a kid?
u: (  you * as a kid ) ^reuse ( HAPPY )

#!!K what scares you?
u: ( [ scare afraid ] ) ^reuse ( SCARE )

#! what is your earliest childhood memory?
u: ( << you [ early childhood ] memory >> ) I remember playing with an HO railroad set. 

#! tell me your history
u: ( your history ) I was born. I survived childhood. I became an adult. 

?: (talk * fish? >) Sure we can talk about fish. ^respond(~FISH)